plugins {
    id 'base'
    id 'com.gradleup.shadow' version '8.3.5' apply false
}

allprojects {
    group = 'dev.ua.theroer'
    version = project.properties.get('version') ?: '0.0.0'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'com.gradleup.shadow'

    // Normalize artifact names to magicutils-*
    def moduleNameMap = [
            'platform-api'       : 'magicutils-api',
            'platform-neoforge'  : 'magicutils-neoforge',
            'platform-bukkit'    : 'magicutils-bukkit',
            'platform-fabric'    : 'magicutils-fabric',
            'core'               : 'magicutils-core',
            'config'             : 'magicutils-config',
            'lang'               : 'magicutils-lang',
            'logger'             : 'magicutils-logger',
            'commands'           : 'magicutils-commands',
            'placeholders'       : 'magicutils-placeholders',
            'commands-fabric'    : 'magicutils-commands-fabric',
            'logger-fabric'      : 'magicutils-logger-fabric',
            'placeholders-fabric': 'magicutils-placeholders-fabric',
            'fabric-bundle'      : 'magicutils-fabric-bundle',
            'config-yaml'        : 'magicutils-config-yaml',
            'config-toml'        : 'magicutils-config-toml',
            'processor'          : 'magicutils-processor'
    ]
    def moduleName = moduleNameMap.getOrDefault(project.name, project.name)

    repositories {
        mavenCentral()
        maven { url = 'https://oss.sonatype.org/content/groups/public/' }
        maven { url = 'https://repo.papermc.io/repository/maven-public/' }
        maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
        maven { url = 'https://maven.nucleoid.xyz' }
        maven { url = 'https://maven.neoforged.net/releases' }
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
        withSourcesJar()
        withJavadocJar()
    }

    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs += ['-Xlint:all,-processing', '-parameters']
    }

    def projectVersion = project.version
    tasks.processResources {
        inputs.property 'version', projectVersion
        filesMatching('fabric.mod.json') {
            expand(version: projectVersion)
        }
    }

    if (project.name != 'processor') {
        dependencies {
            annotationProcessor project(':processor')
        }

        tasks.withType(JavaCompile).configureEach {
            options.compilerArgs += ['-processor',
                    'dev.ua.theroer.magicutils.processor.LogMethodsProcessor,' +
                            'dev.ua.theroer.magicutils.processor.NoOpProcessor,' +
                            'lombok.launch.AnnotationProcessorHider$AnnotationProcessor,' +
                            'lombok.launch.AnnotationProcessorHider$ClaimingProcessor']
        }
    }

    tasks.withType(Jar).configureEach {
        archiveBaseName.set(moduleName)
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
    }

    tasks.withType(com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar).configureEach {
        archiveClassifier.set('all')
        zip64 = true
        mergeServiceFiles()
        if (project.name == 'processor') {
            enabled = false
        }
    }

    if (project.name in ['config', 'config-yaml', 'config-toml', 'lang']) {
        tasks.withType(com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar).configureEach {
            relocate 'com.fasterxml.jackson', 'dev.ua.theroer.magicutils.libs.jackson'
        }

        configurations {
            shadedElements {
                canBeConsumed = true
                canBeResolved = false
                attributes {
                    attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, Usage.JAVA_RUNTIME))
                }
            }
        }

        artifacts {
            shadedElements tasks.named('shadowJar')
        }
    }

    tasks.named('build') {
        dependsOn tasks.named('shadowJar')
    }

    if (project.name != 'platform-fabric' && project.name != 'commands-fabric'
            && project.name != 'logger-fabric' && project.name != 'placeholders-fabric'
            && project.name != 'fabric-bundle') {
        publishing {
            publications {
                mavenJava(MavenPublication) {
                    artifactId = moduleName
                    if (project.name == 'processor') {
                        from components.java
                    } else {
                        from components.java
                    }
                }
                if (project.name != 'processor') {
                    register('mavenShadow', MavenPublication) {
                        artifactId = "${moduleName}-all"
                        artifact tasks.named('shadowJar')
                    }
                }
            }
        }
    }

    tasks.withType(PublishToMavenLocal).configureEach {
        dependsOn tasks.named('shadowJar')
    }
}
