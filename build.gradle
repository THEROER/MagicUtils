plugins {
    id 'base'
    id 'com.gradleup.shadow' version '8.3.5' apply false
}

allprojects {
    group = 'dev.ua.theroer'
    version = project.properties.get('version') ?: '0.0.0'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'com.gradleup.shadow'

    // Normalize artifact names to magicutils-*
    def moduleNameMap = [
            'platform-api'     : 'magicutils-api',
            'platform-neoforge': 'magicutils-neoforge',
            'platform-bukkit'  : 'magicutils-bukkit',
            'core'             : 'magicutils-core',
            'processor'        : 'magicutils-processor'
    ]
    def moduleName = moduleNameMap.getOrDefault(project.name, project.name)

    repositories {
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/groups/public/' }
        maven { url 'https://repo.papermc.io/repository/maven-public/' }
        maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
        maven { url 'https://maven.neoforged.net/releases' }
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
        withSourcesJar()
        withJavadocJar()
    }

    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs += ['-Xlint:all', '-parameters']
    }

    if (project.name != 'processor') {
        dependencies {
            annotationProcessor project(':processor')
        }

        tasks.withType(JavaCompile).configureEach {
            options.compilerArgs += ['-processor',
                    'dev.ua.theroer.magicutils.processor.LogMethodsProcessor,' +
                            'dev.ua.theroer.magicutils.processor.NoOpProcessor,' +
                            'lombok.launch.AnnotationProcessorHider$AnnotationProcessor,' +
                            'lombok.launch.AnnotationProcessorHider$ClaimingProcessor']
        }
    }

    tasks.withType(Jar).configureEach {
        archiveBaseName.set(moduleName)
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
    }

    tasks.withType(com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar).configureEach {
        archiveClassifier.set('all')
        mergeServiceFiles()
        relocate 'org.yaml.snakeyaml', 'dev.ua.theroer.magicutils.shaded.snakeyaml'
        if (project.name == 'processor') {
            enabled = false
        }
    }

    tasks.named('build') {
        dependsOn tasks.named('shadowJar')
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = moduleName
                if (project.name == 'processor') {
                    from components.java
                } else {
                    from components.java
                }
            }
            if (project.name != 'processor') {
                register('mavenShadow', MavenPublication) {
                    artifactId = "${moduleName}-all"
                    artifact tasks.named('shadowJar')
                }
            }
        }
    }

    tasks.withType(PublishToMavenLocal).configureEach {
        dependsOn tasks.named('shadowJar')
    }
}
