plugins {
    id 'fabric-loom' version '1.13.6'
    id 'maven-publish'
}

def minecraftVersion = project.findProperty('minecraft_version') ?: '1.21.10'
def yarnMappings = project.findProperty('yarn_mappings') ?: '1.21.10+build.3'
def loaderVersion = project.findProperty('loader_version') ?: '0.18.3'

configurations {
    bundleShadow {
        canBeConsumed = false
        canBeResolved = true
    }
}

def bundleProjects = [
        ':platform-api',
        ':logger',
        ':commands',
        ':placeholders',
        ':core'
]

def bundleShadedProjects = [
        ':config',
        ':config-yaml',
        ':config-toml',
        ':lang'
]

def bundleRemappedProjects = [
        ':platform-fabric',
        ':logger-fabric',
        ':commands-fabric',
        ':placeholders-fabric'
]

def bundleNamedProjects = [
        ':platform-fabric',
        ':logger-fabric',
        ':commands-fabric',
        ':placeholders-fabric'
]

dependencies {
    minecraft "com.mojang:minecraft:${minecraftVersion}"
    mappings "net.fabricmc:yarn:${yarnMappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${loaderVersion}"

    include 'net.kyori:adventure-api:4.24.0'
    include 'net.kyori:adventure-key:4.24.0'
    include 'net.kyori:adventure-text-minimessage:4.24.0'
    include 'net.kyori:adventure-text-serializer-plain:4.24.0'
    include 'net.kyori:adventure-text-serializer-gson:4.24.0'
    include 'net.kyori:adventure-text-serializer-ansi:4.24.0'
    include 'net.kyori:adventure-text-serializer-json:4.24.0'
    include 'net.kyori:ansi:1.1.1'
    include 'net.kyori:examination-api:1.3.0'
    include 'net.kyori:examination-string:1.3.0'
    include 'net.kyori:option:1.1.0'

    bundleShadedProjects.each { dep ->
        include project(path: dep, configuration: 'shadedElements')
    }
    bundleProjects.each { dep ->
        include project(dep)
    }
    bundleRemappedProjects.each { dep ->
        include project(path: dep, configuration: 'jiJRemap')
    }

    bundleShadedProjects.each { dep ->
        bundleShadow project(path: dep, configuration: 'shadedElements')
    }
    bundleProjects.each { dep ->
        bundleShadow project(dep)
    }
    bundleNamedProjects.each { dep ->
        bundleShadow project(path: dep, configuration: 'namedElements')
    }
}

tasks.named('remapJar') {
    zip64 = true
}

tasks.named('shadowJar') {
    archiveClassifier.set('dev')
    configurations = [project.configurations.bundleShadow]
    exclude 'fabric.mod.json'
    mergeServiceFiles()
    from('src/main/resources') {
        include 'fabric.mod.json'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'magicutils-fabric-bundle'
            artifact(tasks.named('remapJar')) {
                builtBy tasks.named('remapJar')
            }
            artifact(tasks.named('sourcesJar'))
            artifact(tasks.named('javadocJar'))
            artifact(tasks.named('shadowJar')) {
                classifier = 'dev'
            }
            pom.withXml {
                def node = asNode()
                node.children().removeAll { it.name() == 'dependencies' }
            }
        }
    }
}

tasks.withType(GenerateModuleMetadata).configureEach {
    enabled = false
}
