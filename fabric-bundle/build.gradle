plugins {
    id 'fabric-loom' version '1.13.6'
}

def minecraftVersion = project.findProperty('minecraft_version') ?: '1.21.10'
def yarnMappings = project.findProperty('yarn_mappings') ?: '1.21.10+build.3'
def loaderVersion = project.findProperty('loader_version') ?: '0.18.3'

configurations {
    bundleShadow {
        canBeConsumed = false
        canBeResolved = true
    }
}

def bundleProjects = [
        ':platform-api',
        ':config',
        ':config-yaml',
        ':config-toml',
        ':lang',
        ':logger',
        ':commands',
        ':placeholders',
        ':core'
]

def bundleNamedProjects = [
        ':platform-fabric',
        ':logger-fabric',
        ':commands-fabric',
        ':placeholders-fabric'
]

dependencies {
    minecraft "com.mojang:minecraft:${minecraftVersion}"
    mappings "net.fabricmc:yarn:${yarnMappings}:v2"
    modCompileOnly "net.fabricmc:fabric-loader:${loaderVersion}"

    bundleProjects.each { dep ->
        bundleShadow project(dep)
    }
    bundleNamedProjects.each { dep ->
        bundleShadow project(path: dep, configuration: 'namedElements')
    }
}

tasks.named('remapJar') {
    dependsOn tasks.named('shadowJar')
    inputFile.set(tasks.named('shadowJar').flatMap { it.archiveFile })
    zip64 = true
}

tasks.named('shadowJar') {
    archiveClassifier.set('dev')
    configurations = [project.configurations.bundleShadow]
    exclude 'fabric.mod.json'
    exclude 'META-INF/jars/**'
    mergeServiceFiles()
    from('src/main/resources') {
        include 'fabric.mod.json'
    }
}

tasks.named('jar') {
    enabled = false
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'magicutils-fabric-bundle'
            artifact(tasks.named('remapJar')) {
                builtBy tasks.named('remapJar')
            }
            artifact(tasks.named('shadowJar')) {
                classifier = 'dev'
            }
            artifact(tasks.named('sourcesJar'))
            artifact(tasks.named('javadocJar'))
            pom.withXml {
                def node = asNode()
                node.children().removeAll { it.name() == 'dependencies' }
            }
        }
    }
}

tasks.withType(GenerateModuleMetadata).configureEach {
    enabled = false
}
